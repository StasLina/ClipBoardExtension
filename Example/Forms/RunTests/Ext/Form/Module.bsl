&НаСервере
Перем ТекущаяГруппа, ТекущаяСтрока, ПроверяемоеЗначение, ЕстьПроблема, ЕстьОшибка, ОжидаетсяОшибка, ТекущийТестПрерван;

&НаСервере
Перем HTTPСоединение, ТекущийКаталог, ИмяФайлаОбработки, ЕстьОшибкиПроблемы;

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Автотестирование", Автотестирование);
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьНаборТестов(ЭтаФорма);
	
	ФайлОбработки = Новый Файл(ОбработкаОбъект.ИспользуемоеИмяФайла);
	ИмяФайлаОбработки = ФайлОбработки.Имя;
	ТекущийКаталог = ФайлОбработки.Путь;
	
	Если Автотестирование Тогда
		Попытка
			СоздатьСоединение();
			ВыполнитьТесты();
		Исключение
			Лог = Новый ЗаписьТекста(ТекущийКаталог + "autotest.log");
			Лог.ЗаписатьСтроку(ИнформацияОбОшибке().Описание);
			Лог.Закрыть();
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Автотестирование Тогда
		ПодключитьОбработчикОжидания("ЗавершитьРаботу", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботу()
	
	ЗавершитьРаботуСистемы(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область МетодыAppVeyor

&НаСервере
Процедура СоздатьСоединение()
	
	ЧтениеТекста = Новый ЧтениеТекста(ТекущийКаталог + "app_port.txt");
	Порт = Число(ЧтениеТекста.Прочитать());
	HTTPСоединение = Новый HTTPСоединение("localhost", Порт);
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьСообщение(Сообщение, Статус, ПОдробно = "")
	
	Структура = Новый Структура;
	Структура.Вставить("message", Строка(Сообщение));
	Структура.Вставить("category", Строка(Статус));
	Структура.Вставить("details", Строка(ПОдробно));
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
	ТекстJSON = ЗаписьJSON.Закрыть();
	
	HTTPЗапрос = Новый HTTPЗапрос("/api/build/messages");
	HTTPЗапрос.УстановитьТелоИзСтроки(ТекстJSON);
	HTTPЗапрос.Заголовки.Вставить("Content-type", "application/json");
	HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьТест(HTTPМетод, ИмяТеста, Статус, Длительность = 0, Подробно = "")
	
	Структура = Новый Структура;
	Структура.Вставить("outcome", Статус);
	Структура.Вставить("testName", ИмяТеста);
	Структура.Вставить("fileName", ИмяФайлаОбработки);
	Структура.Вставить("ErrorMessage", Подробно);
	Структура.Вставить("durationMilliseconds", Длительность);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Структура);
	ТекстJSON = ЗаписьJSON.Закрыть();
	
	HTTPЗапрос = Новый HTTPЗапрос("/api/tests");
	HTTPЗапрос.УстановитьТелоИзСтроки(ТекстJSON);
	HTTPЗапрос.Заголовки.Вставить("Content-type", "application/json");
	HTTPСоединение.ВызватьHTTPМетод(HTTPМетод, HTTPЗапрос);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункции

&НаСервере
Функция ПрочитатьДанныеJSON(ТекстJSON)
	
	ПоляДаты = Новый Массив;
	ПоляДаты.Добавить("CreationDate");
	ПоляДаты.Добавить("date");
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	Возврат ПрочитатьJSON(ЧтениеJSON, , ПоляДаты);
	
КонецФункции

&НаСервере
Функция ЗаписатьПроблему(ТекстПроблемы)

	ЕстьПроблема = Истина;
	ЕстьОшибкиПроблемы = Истина; 
	
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока = ТекущаяГруппа.ПолучитьЭлементы().Добавить();
		ТекущаяСтрока.Наименование = "Неизвестная проблема";
	КонецЕсли;
	
	ТекущаяСтрока.Подробности = ТекстПроблемы;
	
	Если ТекущаяСтрока.КартинкаСтрок <> 3 Тогда
		ТекущаяСтрока.КартинкаСтрок = 2;
	КонецЕсли;
	
	Если ТекущаяГруппа.КартинкаСтрок <> 3 Тогда
		ТекущаяГруппа.КартинкаСтрок = 2;
	КонецЕсли;
	
	Если Автотестирование Тогда
		ОтправитьСообщение(ТекущаяСтрока.Наименование, "Warning", ТекстПроблемы);
	КонецЕсли;	
	
	Возврат ЭтаФорма;
	
КонецФункции

&НаСервере
Функция ПрерватьТест(Результат, Подробности)
	
	ЕстьОшибка = Истина;
	ЕстьОшибкиПроблемы = Истина; 
	ТекущийТестПрерван = Истина;
	
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока = ТекущаяГруппа.ПолучитьЭлементы().Добавить();
		ТекущаяСтрока.Наименование = "Неизвестная ошибка";
	КонецЕсли;
	
	ТекущаяСтрока.Эталон = Результат;
	ТекущаяСтрока.Подробности = Подробности;
	ТекущаяСтрока.КартинкаСтрок = 3;
	ТекущаяГруппа.КартинкаСтрок = 3;
	
	Если Автотестирование Тогда
		ОтправитьСообщение(ТекущаяСтрока.Наименование, "Error", Подробности);
	КонецЕсли;	
	
	Возврат ЭтаФорма;
	
КонецФункции

#КонецОбласти

#Область ЭкспортныеМетоды

&НаСервере
Процедура Добавить(Знач ИмяМетода, Знач Параметры = Неопределено, Знач Представление = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Параметры) ИЛИ ТипЗнч(Параметры) <> Тип("Массив") Тогда
		Если ТипЗнч(Параметры) = Тип("Строка") И Представление = "" Тогда
			Представление = Параметры;
		КонецЕсли;
		Параметры = Неопределено;
	КонецЕсли;
	
	ТекущаяСтрока = Результаты.ПолучитьЭлементы().Добавить();
	ТекущаяСтрока.Наименование = Представление;
	ТекущаяСтрока.ИмяМетода = ИмяМетода;
	
КонецПроцедуры

&НаСервере
Функция Тест(Знач Представление = "") Экспорт
	
	ТекущийТестПрерван = Ложь;
	ОжидаетсяОшибка = Ложь;
	
	ТекущаяСтрока = ТекущаяГруппа.ПолучитьЭлементы().Добавить();
	ТекущаяСтрока.Наименование = Представление;
	ТекущаяСтрока.КартинкаСтрок = 1;
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция Что(Знач Значение) Экспорт
	
	ПроверяемоеЗначение = Значение;
	ТекущаяСтрока.Результат = Значение;
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция Ошибка(Знач Значение = Неопределено) Экспорт
	
	Если Значение <> Неопределено Тогда
		ПроверяемоеЗначение = Значение;
		ТекущаяСтрока.Результат = Значение;
	КонецЕсли;
	
	ОжидаетсяОшибка = Истина;
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция Вернуть() Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПроверяемоеЗначение;
	
КонецФункции	

&НаСервере
Функция JSON() Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ПустаяСтрока(ПроверяемоеЗначение) Тогда
		ПроверяемоеЗначение = Неопределено;
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ТипЗнч(ПроверяемоеЗначение) <> Тип("Строка") Тогда
		Возврат ПрерватьТест(ПроверяемоеЗначение, "Проверяемое значение не является строкой JSON");
	КонецЕсли;
	
	Попытка
		ПроверяемоеЗначение = ПрочитатьДанныеJSON(ПроверяемоеЗначение);
	Исключение
		Возврат ПрерватьТест(ПроверяемоеЗначение, "Не удалось получить прочитать JSON");
	КонецПопытки;
	
	Возврат ЭтаФорма;
	
КонецФункции

&НаСервере
Функция Base64() Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Попытка
		ПроверяемоеЗначение = Base64Значение(ПроверяемоеЗначение);
	Исключение
		Возврат ПрерватьТест(ПроверяемоеЗначение, "Не удалось прочитать двоичные данные");
	КонецПопытки;
	
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция Успешно() Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	ТекущаяСтрока.Эталон = "{success: true}";
	
	Если ТипЗнч(ПроверяемоеЗначение) <> Тип("Строка") Тогда
		Возврат ПрерватьТест(ПроверяемоеЗначение, "Проверяемое значение не является строкой JSON");
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ПроверяемоеЗначение);
	ДанныеJSON = ПрочитатьJSON(ЧтениеJSON);
	
	ПроверкаНеПройдена = Истина;
	Если ТипЗнч(ДанныеJSON) = Тип("Структура") Тогда
		РезультатПроверки = Неопределено;
		Если ДанныеJSON.Свойство("success", РезультатПроверки) Тогда
			Если РезультатПроверки = Истина Тогда
				ПроверкаНеПройдена = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ПроверкаНеПройдена Тогда
		Возврат ЗаписатьПроблему("Проверяемое значение не соответствует эталону");
	КонецЕсли;
	
	Возврат ЭтаФорма;
	
КонецФункции

&НаСервере
Функция Результат() Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ТипЗнч(ПроверяемоеЗначение) <> Тип("Строка") Тогда
		Возврат ПрерватьТест(ПроверяемоеЗначение, "Проверяемое значение не является строкой JSON");
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ПроверяемоеЗначение);
	ДанныеJSON = ПрочитатьJSON(ЧтениеJSON);
	
	Если Не ДанныеJSON.Свойство("result", ПроверяемоеЗначение) Тогда
		Возврат ПрерватьТест(ПроверяемоеЗначение, "Не удалось получить результат из данных JSON");
	КонецЕсли;
	
	Возврат ЭтаФорма;
	
КонецФункции

&НаСервере
Функция Получить(Знач Имя1 = Неопределено, Знач Имя2 = Неопределено, Знач Имя3 = Неопределено, Знач Имя4 = Неопределено, Знач Имя5 = Неопределено) Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекущаяСтрока.Наименование) Тогда
		ТекущаяСтрока.Наименование = "Получить: " + Имя1;
	КонецЕсли;
	
	Если ТипЗнч(ПроверяемоеЗначение) = Тип("Строка") Тогда
		Попытка
			ПроверяемоеЗначение = ПрочитатьДанныеJSON(ПроверяемоеЗначение);
		Исключение
			Возврат ПрерватьТест(ПроверяемоеЗначение, "Проверяемое значение не является строкой JSON");
		КонецПопытки;
	КонецЕсли;
	
	Попытка
		ПроверяемоеЗначение = ПроверяемоеЗначение[Имя1];
		ТекущаяСтрока.Результат = ПроверяемоеЗначение;
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекстОшибки = "Должна была возинкнуть ошибка при получении значения по индексу: " + Строка(Имя1);
			Возврат ПрерватьТест(ПроверяемоеЗначение, ТекстОшибки);
		КонецЕсли;
	Исключение
		Информация = ИнформацияОбОшибке();
		ОписаниеОшибки = КраткоеПредставлениеОшибки(Информация);
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекущаяСтрока.Результат = ОписаниеОшибки;
		Иначе
			ТекстОшибки = "Не удалось получить значение по индексу: " + Строка(Имя1);
			Возврат ПрерватьТест(ОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецПопытки;
	
	Если Имя2 = Неопределено Тогда
		Возврат ЭтаФорма;
	Иначе
		Возврат Получить(Имя2, Имя3, Имя4, Имя5, Неопределено);
	КонецЕсли;
	
КонецФункции	

&НаСервере
Функция Установить(Знач Имя, Знач Значение) Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекущаяСтрока.Наименование) Тогда
		ТекущаяСтрока.Наименование = "Установить: " + Имя;
	КонецЕсли;
	
	ТекущаяСтрока.Результат = Значение;

	Попытка
		ПроверяемоеЗначение[Имя] = Значение;
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекстОшибки = "Должна была возинкнуть ошибка при установке значения по индексу: " + Строка(Имя);
			Возврат ПрерватьТест(ПроверяемоеЗначение, ТекстОшибки);
		КонецЕсли;
	Исключение
		Информация = ИнформацияОбОшибке();
		ОписаниеОшибки = КраткоеПредставлениеОшибки(Информация);
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекущаяСтрока.Результат = ОписаниеОшибки;
		Иначе
			ТекстОшибки = "Не удалось установить значение по индексу: " + Строка(Имя);
			Возврат ПрерватьТест(ОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецПопытки;
	
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция ПолучитьФормулу(Знач Имя, Знач П1 = "ПУСТО", Знач П2 = "ПУСТО", Знач П3 = "ПУСТО", Знач П4 = "ПУСТО", Знач П5 = "ПУСТО", Знач П6 = "ПУСТО", Знач П7 = "ПУСТО")
	
	Формула = "";
	Количество = 7;
	Для Номер = 0 По Количество - 1 Цикл
		ИмяПараметра = "П" + (Количество - Номер);
		Если Не ПустаяСтрока(Формула) Тогда
			Формула = "," + Формула;
		КонецЕсли;
		Если Вычислить(ИмяПараметра) <> "ПУСТО" Тогда
			Формула = ИмяПараметра + Формула;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "ПроверяемоеЗначение." + Имя + "(" + Формула + ")";
	
КонецФункции	

&НаСервере
Функция Функц(Знач Имя, Знач П1 = "ПУСТО", Знач П2 = "ПУСТО", Знач П3 = "ПУСТО", Знач П4 = "ПУСТО", Знач П5 = "ПУСТО", Знач П6 = "ПУСТО", Знач П7 = "ПУСТО") Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекущаяСтрока.Наименование) Тогда
		ТекущаяСтрока.Наименование = "Функция: " + Имя;
	КонецЕсли;
	
	Попытка
		Формула = ПолучитьФормулу(Имя, П1, П2, П3, П4, П5, П6, П7);
		ПроверяемоеЗначение = Вычислить(Формула);
		ТекущаяСтрока.Результат = ПроверяемоеЗначение;
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекстОшибки = "Должна была возинкнуть ошибка при вызове функции: " + Строка(Имя);
			Возврат ПрерватьТест(ПроверяемоеЗначение, ТекстОшибки);
		КонецЕсли;
	Исключение
		Информация = ИнформацияОбОшибке();
		ОписаниеОшибки = КраткоеПредставлениеОшибки(Информация);
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекущаяСтрока.Результат = ОписаниеОшибки;
		Иначе
			ТекстОшибки = "Не удалось выполнить функцию: " + Строка(Имя);
			Возврат ПрерватьТест(ОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецПопытки;
	
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция Проц(Знач Имя, Знач П1 = "ПУСТО", Знач П2 = "ПУСТО", Знач П3 = "ПУСТО", Знач П4 = "ПУСТО", Знач П5 = "ПУСТО", Знач П6 = "ПУСТО", Знач П7 = "ПУСТО") Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекущаяСтрока.Наименование) Тогда
		ТекущаяСтрока.Наименование = "Процедура: " + Имя;
	КонецЕсли;
	
	Попытка
		Формула = ПолучитьФормулу(Имя, П1, П2, П3, П4, П5, П6, П7);
		Выполнить(Формула);
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекстОшибки = "Должна была возинкнуть ошибка при вызове процедуры: " + Строка(Имя);
			Возврат ПрерватьТест(ПроверяемоеЗначение, ТекстОшибки);
		КонецЕсли;
	Исключение
		Информация = ИнформацияОбОшибке();
		ОписаниеОшибки = КраткоеПредставлениеОшибки(Информация);
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекущаяСтрока.Результат = ОписаниеОшибки;
		Иначе
			ТекстОшибки = "Не удалось выполнить процедуру: " + Строка(Имя);
			Возврат ПрерватьТест(ОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецПопытки;
	
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция Равно(Знач Значение) Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	ТекущаяСтрока.Эталон = Строка(Значение);
	Если ПроверяемоеЗначение <> Значение Тогда
		ЗаписатьПроблему("Проверяемое значение <" + Строка(ПроверяемоеЗначение) + "> не равно эталону <" + Строка(Значение) + ">");
	КонецЕсли;
	
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция ЕстьИстина() Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	ТекущаяСтрока.Эталон = "Значение = Истина";
	Если ПроверяемоеЗначение <> Истина Тогда
		Возврат ЗаписатьПроблему("Проверяемое значение есть ""Ложь"", ожидается ""Истина""");
	КонецЕсли;
	
	Возврат ЭтаФорма;
	
КонецФункции	

&НаСервере
Функция ИмеетТип(Знач ТипИлиИмяТипа) Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	ОжидаемыйТип = ?(ТипЗнч(ТипИлиИмяТипа) = Тип("Строка"), Тип(ТипИлиИмяТипа), ТипИлиИмяТипа);
	ТекущаяСтрока.Эталон = "ТипЗнч(Значение) = Тип(""" + Строка(ТипИлиИмяТипа) + """)";
	ТипПроверяемогоЗначения = ТипЗнч(ПроверяемоеЗначение);
	Если ТипПроверяемогоЗначения <> ОжидаемыйТип Тогда
		Возврат ЗаписатьПроблему("Неверный тип значения: " + ПроверяемоеЗначение);
	КонецЕсли;
	
	Возврат ЭтаФорма;
	
КонецФункции

&НаСервере
Функция Больше(Знач Значение) Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	ТекущаяСтрока.Эталон = "Значение > " + Строка(Значение);
	
	Если Не (ПроверяемоеЗначение > Значение) Тогда
		Возврат ЗаписатьПроблему("Проверяемое значение должно быть больше " + Строка(Значение));
	КонецЕсли;
	
	Возврат ЭтотОбъект;
	
КонецФункции

&НаСервере
Функция ЭтоКартинка(Знач Формат = Неопределено) Экспорт
	
	Если ТекущийТестПрерван Тогда
		Возврат ЭтаФорма;
	КонецЕсли;
	
	Если Формат = Неопределено Тогда
		Формат = ФорматКартинки.PNG;
	КонецЕсли;
	
	Попытка
		ПроверяемаяКартинка = Новый Картинка(ПроверяемоеЗначение);
		Если ПроверяемаяКартинка.Формат() <> ФорматКартинки.PNG Тогда
			ЗаписатьПроблему("Формат картинки не соответствует ожидаемому: " + ФорматКартинки.PNG);
		КонецЕсли;
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекстОшибки = "Должна была возинкнуть ошибка при проверке картинки";
			Возврат ПрерватьТест(ПроверяемоеЗначение, ТекстОшибки);
		КонецЕсли;
	Исключение
		Информация = ИнформацияОбОшибке();
		ОписаниеОшибки = КраткоеПредставлениеОшибки(Информация);
		Если ОжидаетсяОшибка Тогда
			ОжидаетсяОшибка = Ложь;
			ТекущаяСтрока.Результат = ОписаниеОшибки;
		Иначе
			ТекстОшибки = "Не получить значение как картинку";
			Возврат ПрерватьТест(ОписаниеОшибки, ТекстОшибки);
		КонецЕсли;
	КонецПопытки;
	
	Возврат ЭтаФорма;
	
КонецФункции	

#КонецОбласти

&НаСервере
Процедура ВыполнитьТест(ОбработкаОбъект)
	
	ЕстьОшибка = Ложь;
	ЕстьПроблема = Ложь;
	ОжидаетсяОшибка = Ложь;
	ТекущийТестПрерван = Ложь;
	ТекущаяСтрока = Неопределено;
	
	Попытка
		ТекущаяГруппа.КартинкаСтрок = 1;
		ТекущаяГруппа.ПолучитьЭлементы().Очистить();
		ВремяСтарта = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Выполнить("ОбработкаОбъект." + ТекущаяГруппа.ИмяМетода + "(ЭтаФорма)");
	Исключение
		Информация = ИнформацияОбОшибке();
		Результат = КраткоеПредставлениеОшибки(Информация);
		Подробности = ПодробноеПредставлениеОшибки(Информация);
		ПрерватьТест(Результат, Подробности);
	КонецПопытки;
	
	Если Автотестирование Тогда
		Статус = ?(ЕстьОшибка ИЛИ ЕстьПроблема, "Failed", "Passed");
		Длительность = ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяСтарта;
		ОтправитьТест("PUT", ТекущаяГруппа.Наименование, Статус, Длительность);
	КонецЕсли;
	
КонецПроцедуры	
 
&НаСервере
Процедура ВыполнитьТесты()
	
	ЕстьОшибкиПроблемы = Ложь;
	Результаты.ПолучитьЭлементы().Очистить();
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьНаборТестов(ЭтаФорма);
	
	Если Автотестирование Тогда
		Для каждого ТекСтр Из Результаты.ПолучитьЭлементы() Цикл
			ОтправитьТест("POST", ТекСтр.Наименование, "Running");
		КонецЦикла;
	КонецЕсли;
	
	Для каждого ТекСтр Из Результаты.ПолучитьЭлементы() Цикл
		ТекущаяГруппа = ТекСтр;
		ВыполнитьТест(ОбработкаОбъект);
	КонецЦикла;
	
	Если Автотестирование И НЕ ЕстьОшибкиПроблемы Тогда
		ЗаписьТекста = Новый ЗаписьТекста(ТекущийКаталог + "success.txt");
		ЗаписьТекста.ЗаписатьСтроку(ТекущаяУниверсальнаяДата());
		ЗаписьТекста.Закрыть();
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ВыполнитьВыделенные()
	
	ЕстьОшибкиПроблемы = Ложь;
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ВыделенныеСтроки = Элементы.Результаты.ВыделенныеСтроки;
	Для каждого ТекСтр Из Результаты.ПолучитьЭлементы() Цикл
		Ид = ТекСтр.ПолучитьИдентификатор();
		Если ВыделенныеСтроки.Найти(Ид) <> Неопределено Тогда
			ТекущаяГруппа = ТекСтр;
			ВыполнитьТест(ОбработкаОбъект);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура ТолькоВыделенные(Команда)
	
	ВыполнитьВыделенные();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВсе(Команда)
	
	ВыполнитьТесты();
	
КонецПроцедуры
